## 减少虚化带来的负担

在对IL2CPP的阐述当中，我们已经看到了虚方法调用在C++部分的表现。因此，如何减少虚方法的调用也能够在一定程度上改善性能。

不幸的是，.Net编译器通常倾向于生成虚方法调用。事实上，对于多数实例方法，都会通过callvirt指令调用。观察下面代码：
```cshrap
UnityEngine.Debug.LogWarning(subInstance.SampleMethod());
UnityEngine.Debug.LogWarning(subInstance.NonvirtualMethod());
UnityEngine.Debug.LogWarning(SampleSubclass.SampleClassMethod(null, default(int)));
```
他们生成如下的IL代码：
```msil
    // [15 5 - 15 61]
    IL_0006: ldloc.0      // sampleSubclass
    IL_0007: callvirt     instance string Assets.Scripts.SampleSubclass::SampleMethod()
    IL_000c: call         void [UnityEngine]UnityEngine.Debug::LogWarning(object)

    // [16 5 - 16 65]
    IL_0011: ldloc.0      // sampleSubclass
    IL_0012: callvirt     instance string Assets.Scripts.SampleSubclass::NonvirtualMethod()
    IL_0017: call         void [UnityEngine]UnityEngine.Debug::LogWarning(object)

    // [17 5 - 17 90]
    IL_001c: ldnull       
    IL_001d: ldc.i4.0     
    IL_001e: call         string Assets.Scripts.SampleSubclass::SampleClassMethod(class Assets.Scripts.SampleSubclass, int32)
    IL_0023: call         void [UnityEngine]UnityEngine.Debug::LogWarning(object)
```
非但是重写基类的SampleMethod方法，连NonvirtualMethod方法都是使用callvirt调用的。

然而针对IL2CPP，还有方法可以让方法避免作为虚方法被调用。其方法是为类或者方法加上sealed，使其成为密闭的。在为SampleSubClass加上sealed关键字之后，生成的上述方法调用的IL2CPP代码如下：
```cpp
SampleSubclass_t_313122036_0 * L_1 = V_0;
String_t* L_2 = SampleSubclass_SampleMethod_m_90972724_0(L_1, /*hidden argument*/NULL);
IL2CPP_RUNTIME_CLASS_INIT(Debug_t2108915146_0_il2cpp_TypeInfo_var);
Debug_LogWarning_m_1791389328_0(NULL /*static, unused*/, L_2, /*hidden argument*/NULL);
SampleSubclass_t_313122036_0 * L_3 = V_0;
String_t* L_4 = SampleSubclass_NonvirtualMethod_m1104769664_0(L_3, /*hidden argument*/NULL);
Debug_LogWarning_m_1791389328_0(NULL /*static, unused*/, L_4, /*hidden argument*/NULL);
String_t* L_5 = SampleSubclass_SampleClassMethod_m_687551974_0(NULL /*static, unused*/, (SampleSubclass_t_313122036_0 *)NULL, 0, /*hidden argument*/NULL);
Debug_LogWarning_m_1791389328_0(NULL /*static, unused*/, L_5, /*hidden argument*/NULL);
```
参考前文，虚调用已经没有了。

另外，类方法是始终不会作为虚方法被调用的。

## 避免装箱

当需要在期望使用堆对象的 情况下使用栈对象时，就会导致装箱。装箱会导致新的堆对象被分配出来，而且其寿命通常很短，因此会导致较多的GC操作，因此比较被大家讨厌。以下列出了一些导致装箱的情况，其中有些不太常为大家所知，还请注意。

在任何以引用类型的隐喻使用值类型的场景下，装箱都会被调用。大多数情况下，这些现象并非很隐秘，诸如：

```csharp
    object boxedInt = 1;
```
或是
```csharp
void BoxSample(object papam)
{
    // ...do something
}

void Main(string[] params)
{
    BoxSample(10)
}
```
这样。如果确定此处的装箱并非必须的，可以通过简单的方法去除这里的装箱操作——使用泛型。

然而有时装箱操作并非那么明显。考虑如下代码：
```csharp
public class BoxSample
    {
        public void SampleMethod()
        {
            PureStruct member = new PureStruct();
            member.ToString();

            GenericMethod(member);
        }

        public bool GenericMethod<T>(T argu)
        {
            return argu != null;
        }
    }

    public struct PureStruct
    {
        public int Field;
    }
```

它们当中两个方法产生如下的IL代码：
```msil
.method public hidebysig instance void
    SampleMethod() cil managed
  {
    .maxstack 5
    .locals init (
      [0] valuetype Assets.Scripts.PureStruct argu
    )

    // [13 7 - 13 41]
    IL_0000: ldloca.s     argu
    IL_0002: initobj      Assets.Scripts.PureStruct

    // [14 7 - 14 22]
    IL_0008: ldloc.0      // argu
    IL_0009: box          Assets.Scripts.PureStruct     // OUCH!!!
    IL_000e: callvirt     instance string [mscorlib]System.ValueType::ToString()
    IL_0013: pop          

    // [15 7 - 15 43]
    IL_0014: ldarg.0      // this
    IL_0015: ldloc.0      // argu
    IL_0016: call         instance bool Assets.Scripts.BoxSample::GenericMethod<valuetype Assets.Scripts.PureStruct>(!!0/*valuetype Assets.Scripts.PureStruct*/)
    IL_001b: pop          
    IL_001c: ret          

  } // end of method BoxSample::SampleMethod

  .method public hidebysig instance bool
    GenericMethod<T>(
      !!0/*T*/ argu
    ) cil managed
  {
    .maxstack 8

    // [20 7 - 20 36]
    IL_0000: ldarg.1      // argu
    IL_0001: box          !!0/*T*/  // OUCH!!!
    IL_0006: ldnull       
    IL_0007: ceq          
    IL_0009: ldc.i4.0     
    IL_000a: ceq          
    IL_000c: ret          

  } // end of method BoxSample::GenericMethod
```
____
注意！有两处产生了装箱！其中一处源于对我们的结构PureStruct调用来自object的方法，另一次则是在泛型方法里对其进行判空。因为ToString方法是属于Object类型的方法，而Object类型是一个引用类型，因此第一处装箱的位置方法调用暗喻了对象的引用身份；第二处则是因为值类型不可能为null（可空类型重写了==操作符，暂不在此讨论范围之内）。

对于第一种情况，我们重写PureStruct结构的ToString方法：
```csharp
    public struct PureStruct
    {
        public int Field;

        public override string ToString()
        {
            return "Maru 9";
        }
    }
```
再看看SampleMethod的IL代码：
```csharp.method
  public hidebysig instance void
    SampleMethod() cil managed
  {
    .maxstack 5
    .locals init (
      [0] valuetype Assets.Scripts.PureStruct argu
    )

    // [13 7 - 13 41]
    IL_0000: ldloca.s     argu
    IL_0002: initobj      Assets.Scripts.PureStruct

    // [14 7 - 14 22]
    IL_0008: ldloca.s     argu
    IL_000a: call         instance string Assets.Scripts.PureStruct::ToString()
    IL_000f: pop          

    // [15 7 - 15 43]
    IL_0010: ldarg.0      // this
    IL_0011: ldloc.0      // argu
    IL_0012: call         instance bool Assets.Scripts.BoxSample::GenericMethod<valuetype Assets.Scripts.PureStruct>(!!0/*valuetype Assets.Scripts.PureStruct*/)
    IL_0017: pop          
    IL_0018: ret          

  } // end of method BoxSample::SampleMethod
```
___
好的，装箱没有了——非但如此，ToString甚至不被认为是个虚函数了。毕竟值类型是没法继承的。

对于第二种情况，如有条件则直接指定泛型参数的约束：
```csharp
public bool GenericMethod<T>(T argu) where T : struct
```

现在你甚至不能对argu参数判空了。

## 切换编译器

还是先来认识一下残酷的现实，从foreach在Unity中的低性能说起。

编写一个简单的示例代码：
```csharp
        public void ForeachSample()
        {
            List<int> list = new List<int>();
            foreach (var value in list)
            {

            }
        }
```
并在Unity当中将其编译后，看看生成的il代码：

```msil
    .method public hidebysig instance void
    ForeachSample() cil managed
  {
    .maxstack 8
    .locals init (
      [0] class [mscorlib]System.Collections.Generic.List`1<int32> V_0,
      [1] int32 current,
      [2] valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32> V_2
    )

    // [71 7 - 71 80]
    IL_0000: newobj       instance void class [mscorlib]System.Collections.Generic.List`1<int32>::.ctor()
    IL_0005: stloc.0      // V_0

    IL_0006: ldloc.0      // V_0
    IL_0007: callvirt     instance valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<!0/*int32*/> class [mscorlib]System.Collections.Generic.List`1<int32>::GetEnumerator()
    IL_000c: stloc.2      // V_2
    .try
    {
      IL_000d: br           IL_001a
      // start of loop, entry point: IL_001a

        // [75 11 - 75 43]
        IL_0012: ldloca.s     V_2
        IL_0014: call         instance !0/*int32*/ valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>::get_Current()
        IL_0019: stloc.1      // current

        // [73 9 - 73 38]
        IL_001a: ldloca.s     V_2
        IL_001c: call         instance bool valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>::MoveNext()
        IL_0021: brtrue       IL_0012
      // end of loop

      IL_0026: leave        IL_0037
    } // end of .try
    finally
    {
      IL_002b: ldloc.2      // V_2
      IL_002c: box          valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>
      IL_0031: callvirt     instance void [mscorlib]System.IDisposable::Dispose()
      IL_0036: endfinally   
    } // end of finally
    IL_0037: ret          

  } // end of method SampleSubclass::ForeachSample
```

再用VisualStudio编译相同的代码，生成的il如下：
```msil
.method public hidebysig instance void
  ForeachSample() cil managed
{
  .maxstack 1
  .locals init (
    [0] class [mscorlib]System.Collections.Generic.List`1<int32> list,
    [1] valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32> V_1,
    [2] int32 'value'
  )

  // [77 9 - 77 10]
  IL_0000: nop          

  // [78 13 - 78 46]
  IL_0001: newobj       instance void class [mscorlib]System.Collections.Generic.List`1<int32>::.ctor()
  IL_0006: stloc.0      // list

  // [79 13 - 79 20]
  IL_0007: nop          

  // [79 35 - 79 39]
  IL_0008: ldloc.0      // list
  IL_0009: callvirt     instance valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<!0/*int32*/> class [mscorlib]System.Collections.Generic.List`1<int32>::GetEnumerator()
  IL_000e: stloc.1      // V_1
  .try
  {

    IL_000f: br.s         IL_001b
    // start of loop, entry point: IL_001b

      // [79 22 - 79 31]
      IL_0011: ldloca.s     V_1
      IL_0013: call         instance !0/*int32*/ valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>::get_Current()
      IL_0018: stloc.2      // 'value'

      // [80 13 - 80 14]
      IL_0019: nop          

      // [82 13 - 82 14]
      IL_001a: nop          

      // [79 32 - 79 34]
      IL_001b: ldloca.s     V_1
      IL_001d: call         instance bool valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>::MoveNext()
      IL_0022: brtrue.s     IL_0011
    // end of loop
    IL_0024: leave.s      IL_0035
  } // end of .try
  finally
  {

    IL_0026: ldloca.s     V_1
    IL_0028: constrained. valuetype [mscorlib]System.Collections.Generic.List`1/Enumerator<int32>
    IL_002e: callvirt     instance void [mscorlib]System.IDisposable::Dispose()
    IL_0033: nop          
    IL_0034: endfinally   
  } // end of finally

  // [83 9 - 83 10]
  IL_0035: ret          

} // end of method SampleSubclass::ForeachSample
```

注意Unity生成的代码中IL_002c处的装箱操作，该操作在微软编译器生成的代码中不存在。foreach所使用的IEnumerator通常是一个实现了该接口的结构体，因此在Unity所使用的未经优化的编译器中构成了装箱操作，这个操作成为foreach操作堆性能问题的元凶。

这只是冰山一角。C#编译器中积累了大量的优化，这些优化在有十余年历史的Unity Mono 2.6编译器中均不存在。一个改善Unity生成性能的简单的方法，是直接绕过Unity自身携带的编译器。

一个简单的办法如下：
1. 在Unity项目的相同解决方案下新建一个项目，确保类型为“类库”；
2. 修改其生成路径到Unity项目中保存外部库的路径；
3. 生成步骤1当中建立的类库。

因为在同一解决方案中并且维护有相互之间的引用，调试时VisualStudio能够正确地进入类库的源码中。同时，新建的类库又能享受更官方的编译器带来的优势。

另一个较为激进的方案可以通过[这里](https://bitbucket.org/alexzzzz/unity-c-5.0-and-6.0-integration/src)查看。这个方案中甚至使Unity通过使用更新的编译器支持到C# 6的语言特性。

## 其他

### 为集合预分配容量

数组以外的大多数集合都会在集合长度达到其容量的某一比例（通常为72%）是重新分配其内存。如果集合的长度大致可以预期，应当在建立集合时指定集合容量，从而减少集合的内存分配，同时，因为内存个追加分配时容量会越来越大，此举亦可以减少内存空间的浪费。

### 不要使用空析构方法

进行GC时，所有拥有析构方法的对象会被排入F-Sequence并在本轮GC完成之后调用其析构方法，因此直到下轮GC才能够释放。为了确保对象的内存能够及时释放，如果没有必要就不应当为对象指定析构方法。

### 重写值类型的Equals和GetHashCode方法

这两个方法的默认实现使用了反射，性能较差。注意，不只是比较，即便是将对象放入字典也需要调用这两个方法。

### 使用String.Format而不要使用操作符拼接字符串

String.Format使用StringBuilder，组合字符串的全过程只会重新分配一次内存。

### 使用Resharper

效果立竿见影。Resharper能够很敏感地发现代码中的潜在问题，也能够根据设定指出格式上的差误。如果能够确保所有Resharper指出的代码问题都被解决，就算Coverity再发现什么也大可以当成是误报。
